// SMTP OAuth Relay Azure Deployment
// Deploys: Azure Container Instance, Storage Account with Table (optional)

@description('Location for all resources')
param location string = resourceGroup().location

@description('Name of the Azure Container Group')
param containerGroupName string

@description('Container image version to deploy')
param containerVersion string = '1'

@allowed([
  'off'
  'keyvault'
])
@description('TLS source for SMTP connections')
param tlsSource string = 'off'

@description('Key Vault URL where the TLS certificate is stored (required when tlsSource=keyvault)')
param tlsCertKeyVaultUrl string = ''

@description('Name of the TLS certificate to use (required when tlsSource=keyvault)')
param tlsCertName string = ''

@description('Name of the Storage Account to create for Azure Tables (ignored if lookupTableUrl is set)')
param storageAccountName string = toLower('smtprelay${uniqueString(resourceGroup().id)}')

@description('Table name to create for Azure Tables (ignored if lookupTableUrl is set)')
param tableName string = 'users'

@description('URL of an existing Azure Table to use instead of creating one')
param lookupTableUrl string = ''

@description('Additional SMTP OAuth Relay environment variables')
param envVars object = {}

@description('Role definition ID for Storage Table Data Reader')
param storageTableDataReaderRoleId string = '0a9a7e1f-b4e0-4ad0-8b83-1ef0a56a02ff'

var containerImage = 'ghcr.io/justiniven/smtp-oauth-relay:${containerVersion}'
var useExistingTable = lookupTableUrl != ''
var tableUrl = useExistingTable
  ? lookupTableUrl
  : 'https://${storageAccount.name}.table.core.windows.net/${tableName}'
var requireTls = tlsSource != 'off'
var finalEnvVars = {
  REQUIRE_TLS: string(requireTls)
  TLS_SOURCE: tlsSource
  AZURE_TABLES_URL: tableUrl
  ...(tlsSource == 'keyvault' ? {
    AZURE_KEY_VAULT_URL: tlsCertKeyVaultUrl
    AZURE_KEY_VAULT_CERT_NAME: tlsCertName
  } : {})
  ...envVars
}

resource storageAccount 'Microsoft.Storage/storageAccounts@2023-01-01' = if (!useExistingTable) {
  name: storageAccountName
  location: location
  kind: 'StorageV2'
  sku: {
    name: 'Standard_LRS'
  }
  properties: {
    allowBlobPublicAccess: false
    minimumTlsVersion: 'TLS1_2'
  }
}

resource tableService 'Microsoft.Storage/storageAccounts/tableServices@2023-01-01' = if (!useExistingTable) {
  name: '${storageAccount.name}/default'
}

resource lookupTable 'Microsoft.Storage/storageAccounts/tableServices/tables@2023-01-01' = if (!useExistingTable) {
  name: '${storageAccount.name}/default/${tableName}'
  dependsOn: [
    tableService
  ]
}

resource containerGroup 'Microsoft.ContainerInstance/containerGroups@2023-05-01' = {
  name: containerGroupName
  location: location
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    containers: [
      {
        name: 'smtp-oauth-relay'
        properties: {
          image: containerImage
          resources: {
            requests: {
              cpu: 1
              memoryInGB: 2
            }
          }
          environmentVariables: [
            for key in objectKeys(finalEnvVars) : {
              name: key
              value: finalEnvVars[key]
            }
          ]
          ports: [
            {
              protocol: 'TCP'
              port: 8025
            }
          ]
        }
      }
    ]
    restartPolicy: 'OnFailure'
    osType: 'Linux'
    ipAddress: {
      type: 'Public'
      autoGeneratedDomainNameLabelScope: 'SubscriptionReuse'
      dnsNameLabel: toLower(containerGroupName)
      ports: [
        {
          protocol: 'TCP'
          port: 8025
        }
      ]
    }
  }
}

resource tableReaderRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = if (!useExistingTable) {
  name: guid(storageAccount.id, containerGroup.id, 'table-reader')
  scope: storageAccount
  properties: {
    roleDefinitionId: subscriptionResourceId(
      'Microsoft.Authorization/roleDefinitions',
      storageTableDataReaderRoleId
    )
    principalId: containerGroup.identity.principalId
    principalType: 'ServicePrincipal'
  }
}

output azureTableUrl string = tableUrl
output containerGroupFqdn string = containerGroup.properties.ipAddress.fqdn
